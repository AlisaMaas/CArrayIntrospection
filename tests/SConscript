########################################################################
#
#  common imports and setup for all tests
#

Import('env')
env.AppendUnique(CLANG_FLAGS='-Werror')


def RunTest(self, source, json=None, **kwargs):
    source = File(source)
    bitcode = self.BitcodeSource(source)
    if not json:
        json = source.target_from_source('json/', '.json')
        self.IIGlueAnalyze(json, bitcode)
    actual = source.target_from_source('actualsAndExpecteds/', '.actual')
    self.RunPlugin(actual, (bitcode, json), **kwargs)
    passed = self.Expect(actual)
    Alias('test', passed)

env.AddMethod(RunTest)


def RunTests(self, **kwargs):
    for source in Glob('*.c'):
        self.RunTest(source, **kwargs)

env.AddMethod(RunTests)


########################################################################
#
#  IIGlueReader tests
#

tenv = env.Clone(PLUGIN_ARGS='-iiglue-reader')
tenv.RunTest('iiglue-reader-peek.c')
tenv.RunTest('iiglue-reader-variadic.c')
tenv.RunTest('iiglue-reader-multiple.c', json=('json/iiglue-reader-peek.json', 'json/iiglue-reader-variadic.json'))


########################################################################
#
#  subdirectories
#

SConscript(dirs=['sentinelCheckTests'], exports='env')
#SConscript(dirs=['sentinelCheckTests','fixedLengthTests'], exports='env')

# Local variables:
# flycheck-flake8rc: "../scons-flake8.ini"
# End:
