########################################################################
#
#  common imports and setup for all tests
#

Import('env')
env.AppendUnique(CLANG_FLAGS='-Werror')

plugin = env['plugin']


########################################################################
#
#  IIGlueReader tests
#

tests = (
    'peek',
    'variadic',
)

sources = File(['iiglue-reader-%s.c' % tail for tail in tests])

for source in sources:

    # compile C source to LLVM bitcode, then analyze using iiglue
    bitcode = env.BitcodeSource(source)
    json = env.IIGlueAnalyze(bitcode)

    # run IIGlueReader to read saved iiglue output
    actual = env.RunPlugin((bitcode, json), PLUGIN_ARGS='-iiglue-reader')

    # compare IIGlueReader output dump to expected output
    env.Expect(actual)


########################################################################
#
#  common post-setup work for all tests
#

# convenient "scons test" alias to run all tests
Alias('test', Glob('*.passed'))


########################################################################
#
#  subdirectories
#

SConscript(dirs='sentinelCheckTests', exports='env')

# Local variables:
# flycheck-flake8rc: "../scons-flake8.ini"
# End:
