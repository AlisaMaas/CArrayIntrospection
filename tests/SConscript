Import('env')


########################################################################


# compile C source to LLVM bitcode
source = File('iiglue-reader-peek.c')
bitcode = source.target_from_source('', '.bc')
env.Command(
    bitcode, source,
    'clang -emit-llvm -o $TARGET -c $SOURCE',
)

# run IIGlueReader to read saved iiglue output
json = source.target_from_source('', '.json')
plugin = env.File('../${LIBPREFIX}IIGlueReader${SHLIBSUFFIX}')
actual = source.target_from_source('', '.actual')
env.Command(
    actual, (plugin, json, bitcode),
    [
        [
            'opt',
            '-analyze', '-o', '$TARGET',
            '-load', '${SOURCES[0].abspath}',
            '-iiglue-reader', '-iiglue-read-file', '${SOURCES[1]}',
            '${SOURCES[2]}',
        ],
    ],
)

# compare IIGlueReader output dump to expected output
expected = source.target_from_source('', '.expected')
passed = source.target_from_source('', '.passed')
env.Command(
    passed, (actual, expected),
    [
        'cmp --silent $SOURCES',
        Touch('$TARGET'),
    ],
)


########################################################################


# convenient "scons test" alias to run all tests
Alias('test', Glob('*.passed'))


# Local variables:
# flycheck-flake8rc: "../scons-flake8.ini"
# End:
